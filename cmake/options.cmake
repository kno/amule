#
# This file is part of the aMule Project.
#
# Copyright (c) 2011 Werner Mahr (Vollstrecker) <amule@vollstreckernet.de>
#
# Any parts of this program contributed by third-party developers are copyrighted
# by their respective authors.
#
# This program is free software; you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation; either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301, USA
#
# This file contains the options for enabling or disabling parts of aMule, and
# sets the needed variables for them to compile
#

option (BUILD_ALC "compile aLinkCreator GUI version")
option (BUILD_ALCC "compile aLinkCreator for console")
option (BUILD_AMULECMD "compile aMule command line client")

if (UNIX)
	option (BUILD_CAS "compile C aMule Statistics")
endif()

option (BUILD_DAEMON "compile aMule daemon version")
option (BUILD_ED2K "compile aMule ed2k links handler" ON)
option (BUILD_EVERYTHING "compile all parts of aMule")
option (BUILD_FILEVIEW "compile aMule file viewer for console (EXPERIMENTAL)")
option (BUILD_MONOLITHIC "enable building of the monolithic aMule app" ON)
option (BUILD_REMOTEGUI "compile aMule remote GUI")
option (BUILD_WEBSERVER "compile aMule WebServer")
option (BUILD_AMULEAPI "compile aMule REST API daemon")
option (BUILD_WXCAS "compile aMule GUI Statistics")
option (BUILD_TESTING "Build unit tests" OFF)
option (USE_SYSTEM_PICOJSON "Use system-installed picojson instead of bundled copy" OFF)

if (PREFIX)
	set (CMAKE_INSTALL_PREFIX "${PREFIX}")
endif()

include (GNUInstallDirs)

set (PKGDATADIR "${CMAKE_INSTALL_DATADIR}/${PACKAGE}")

if (BUILD_EVERYTHING)
	set (BUILD_ALC ON CACHE BOOL "compile aLinkCreator GUI version" FORCE)
	set (BUILD_ALCC ON CACHE BOOL "compile aLinkCreator for console" FORCE)
	set (BUILD_AMULECMD ON CACHE BOOL "compile aMule command line client" FORCE)

	if (UNIX)
		set (BUILD_CAS ON CACHE BOOL "compile C aMule Statistics" FORCE)
	endif()

	set (BUILD_DAEMON ON CACHE BOOL "compile aMule daemon version" FORCE)
	set (BUILD_FILEVIEW ON CACHE BOOL "compile aMule file viewer for console (EXPERIMENTAL)" FORCE)
	set (BUILD_REMOTEGUI ON CACHE BOOL "compile aMule remote GUI" FORCE)
	set (BUILD_WEBSERVER ON CACHE BOOL "compile aMule WebServer" FORCE)
	set (BUILD_AMULEAPI ON CACHE BOOL "compile aMule REST API daemon" FORCE)
	set (BUILD_WXCAS ON CACHE BOOL "compile aMule GUI Statistics" FORCE)
endif()

if (BUILD_AMULECMD)
	set (NEED_LIB_EC TRUE)
	set (NEED_LIB_MULECOMMON TRUE)
	set (NEED_LIB_MULESOCKET TRUE)
	set (wx_NEED_NET TRUE)
	set (NEED_ZLIB TRUE)
endif()

if (BUILD_AMULEAPI)
	# Mirrors amulecmd's needs: EC connection, mulecommon helpers (Format,
	# MD5Sum), socket lib for CRemoteConnect. Boost.Beast is header-only
	# so we don't add a Boost component requirement; the link-side Boost
	# is already wired via the project-level `Boost_LIBRARIES` lookup.
	#
	# Hard-fail policy. `BUILD_AMULEAPI=YES` + missing dep must fail
	# at configure time, never soft-disable the target. Today the
	# guarantees come from upstream wiring:
	#   * cryptopp — `NEED_LIB_EC` (set below) implies `NEED_LIB_CRYPTO`,
	#     which includes cmake/cryptopp.cmake; that file FATAL_ERRORs
	#     on a missing `cryptlib.h`.
	#   * Boost   — `cmake/boost.cmake` runs unconditionally at the
	#     project root and uses `find_package(Boost CONFIG REQUIRED)`,
	#     which FATAL_ERRORs on miss.
	# If a future refactor breaks either chain (e.g. moves Boost
	# behind a conditional `if`), add an explicit `find_package(Boost
	# CONFIG REQUIRED)` here so amuleapi keeps fail-loud.
	set (NEED_LIB_EC TRUE)
	set (NEED_LIB_MULECOMMON TRUE)
	set (NEED_LIB_MULESOCKET TRUE)
	set (wx_NEED_NET TRUE)
	set (NEED_ZLIB TRUE)
	# Compile-time install path the daemon falls back to when
	# [Server]/StaticRoot is empty in the conf. Mirrors WEBSERVERDIR but
	# uses the ABSOLUTE form so a binary running from /usr/local/bin
	# (or wherever the operator put it) resolves to the matching
	# /usr/local/share/amule/amuleapi-static without needing to be
	# cwd'd at the install prefix.
	set (AMULEAPI_STATIC_DIR
		"${CMAKE_INSTALL_FULL_DATADIR}/${PACKAGE}/amuleapi-static/")
endif()

if (BUILD_CAS)
	set (BUILD_UTIL TRUE)
endif()

if (BUILD_ALCC)
	set (BUILD_UTIL TRUE)
	set (wx_NEED_BASE TRUE)
endif()

if (BUILD_ALC)
	set (BUILD_UTIL TRUE)
	set (wx_NEED_GUI TRUE)
endif()

if (BUILD_DAEMON)
	set (NEED_LIB_EC TRUE)
	set (NEED_LIB_MULEAPPCOMMON TRUE)
	set (NEED_LIB_MULECOMMON TRUE)
	set (NEED_LIB_MULESOCKET TRUE)
	set (NEED_ZLIB TRUE)
	set (wx_NEED_NET TRUE)
endif()

if (BUILD_ED2K)
	set (wx_NEED_BASE TRUE)
endif()

if (BUILD_FILEVIEW)
	set (BUILD_UTIL TRUE)
	set (NEED_LIB_CRYPTO TRUE)
	set (NEED_LIB_MULECOMMON TRUE)
	set (wx_NEED_NET TRUE)
endif()

if (BUILD_MONOLITHIC)
	set (NEED_LIB_EC TRUE)
	set (NEED_LIB_MULEAPPGUI TRUE)
	set (NEED_LIB_MULEAPPCOMMON TRUE)
	set (NEED_LIB_MULECOMMON TRUE)
	set (NEED_LIB_MULESOCKET TRUE)
	set (NEED_ZLIB TRUE)
	set (wx_NEED_ADV TRUE)
	set (wx_NEED_NET TRUE)
endif()

if (BUILD_MONOLITHIC OR BUILD_REMOTEGUI)
	set (INSTALL_SKINS TRUE)
endif()

if (BUILD_REMOTEGUI)
	set (NEED_GLIB_CHECK TRUE)
	set (NEED_LIB_EC TRUE)
	set (NEED_LIB_MULEAPPCOMMON TRUE)
	set (NEED_LIB_MULEAPPGUI TRUE)
	set (NEED_LIB_MULECOMMON TRUE)
	set (NEED_LIB_MULESOCKET TRUE)
	set (NEED_ZLIB TRUE)
	set (wx_NEED_ADV TRUE)
	set (wx_NEED_NET TRUE)
endif()

if (BUILD_WEBSERVER)
	set (NEED_LIB_EC TRUE)
	set (NEED_LIB_MULECOMMON TRUE)
	set (NEED_LIB_MULESOCKET TRUE)
	set (NEED_ZLIB TRUE)
	set (WEBSERVERDIR "${PKGDATADIR}/webserver/")
	set (wx_NEED_NET TRUE)
endif()

if (BUILD_WXCAS)
	set (BUILD_UTIL TRUE)
	set (wx_NEED_GUI TRUE)
	set (wx_NEED_NET TRUE)
endif()

if (NEED_LIB_EC)
	set (NEED_LIB_CRYPTO TRUE)
endif()

if (NEED_LIB_MULECOMMON OR NEED_LIB_EC)
	set (NEED_LIB TRUE)
	set (wx_NEED_BASE TRUE)
endif()

if (NEED_LIB_MULECOMMON)
	set (NEED_GLIB_CHECK TRUE)
endif()

if (NEED_LIB_MULEAPPCOMMON)
	option (ENABLE_IP2COUNTRY "compile with GeoIP IP2Country library" ON)
	# Compile the mmap file-I/O path where the platform supports it (libc
	# capability, no external dependency). Actual use is a runtime preference
	# (MMapEnabled, default OFF). Set OFF only to exclude the mmap code and its
	# SIGSEGV/SIGBUS handler entirely (e.g. sanitizer builds).
	option (ENABLE_MMAP "compile the mmap file-I/O path where supported" ON)
	option (ENABLE_NLS "enable national language support" ON)
	# Backtrace symbol resolution: ON => use libbfd for in-process
	# address→file:line resolution; OFF => fall back to
	# backtrace_symbols() for function names + an external addr2line
	# popen() for line info (MuleDebug.cpp). Off is intended for
	# environments that ship libbfd in the SDK but not in the runtime
	# (e.g. GNOME-Platform-based Flatpak builds, see #13).
	option (ENABLE_BFD "use libbfd for in-process backtrace symbol resolution" ON)
	set (NEED_LIB_MULEAPPCORE TRUE)
	set (wx_NEED_BASE TRUE)
else()
	set (ENABLE_IP2COUNTRY FALSE)
	set (ENABLE_MMAP FALSE)
	set (ENABLE_NLS FALSE)
	set (ENABLE_BFD FALSE)
endif()

if (NEED_LIB_MULEAPPGUI)
	set (wx_NEED_GUI TRUE)
	# The log/server-info panes use wxStyledTextCtrl (Scintilla) for fast,
	# full-history scrolling; only the GUI lib pulls it in.
	set (wx_NEED_STC TRUE)
endif()

if (NEED_LIB_MULESOCKET)
	set (wx_NEED_BASE TRUE)
endif()

# boost::asio is mandatory; the remaining wxWidgets-sockets consumers are
# the daemon/GUI EC paths, the wxcas helper, and amuleweb (which links
# wxWidgets::NET directly in src/webserver/src/CMakeLists.txt for its
# socket code). Keep wx_NEED_NET on only when those are actually being
# built.
if (NOT (BUILD_DAEMON OR BUILD_MONOLITHIC OR BUILD_REMOTEGUI OR BUILD_WEBSERVER OR BUILD_WXCAS OR BUILD_AMULECMD OR BUILD_AMULEAPI))
	set (wx_NEED_NET FALSE)
endif()

if (wx_NEED_ADV OR wx_NEED_BASE OR wx_NEED_GUI OR wx_NEED_NET)
	set (wx_NEEDED TRUE)

	if (WIN32 AND NOT wx_NEED_BASE)
		set (wx_NEED_BASE TRUE)
	endif()
endif()

add_compile_definitions ($<$<CONFIG:DEBUG>:__DEBUG__>)

if (WIN32)
	add_compile_definitions ($<$<CONFIG:DEBUG>:wxDEBUG_LEVEL=0>)
endif (WIN32)

if (NEED_LIB_MULEAPPCOMMON OR BUILD_WEBSERVER)
	option (ENABLE_UPNP "enable UPnP support in aMule" ON)
endif()

# uTP (Micro Transport Protocol) as a transport for ed2k client connections,
# carried over the UDP port the client already uses for ed2k UDP. OFF by
# default: libutp is vendored at src/extern/libutp, but a new transport is
# opt-in until it has run in real use (see the change's staging notes). The
# verification image in packaging/linux/dev/Dockerfile turns it ON so the
# adapter is actually compiled and tested. With it OFF the shim still compiles
# and is
# still unit-tested — only the adapter that calls utp_* is compiled out, and
# the client advertises no uTP capability to peers.
option (ENABLE_UTP "enable the uTP transport (requires libutp)" OFF)
option (USE_SYSTEM_LIBUTP "use a system-installed libutp instead of a bundled copy" OFF)

# QUIC as a NAT-traversal data transport alongside uTP, on the same UDP port and
# behind the same OP_UDPRESERVEDPROT2 envelope (frame type 0x01 rather than
# 0x00). OFF by default, and more firmly so than ENABLE_UTP: this one adds two
# dependencies aMule has never linked -- ngtcp2 and GnuTLS -- and there is no
# bundled copy of either, because vendoring security-relevant code with an
# active vulnerability history is a rejected alternative in this change's
# design. A build without it is a build with no QUIC, never a build that fails
# to compile: the protocol and policy headers still compile and are still unit
# tested, only the adapter that calls ngtcp2_* is compiled out, and the client
# advertises no QUIC capability to peers.
#
# Not viable on every platform aMule ships on, which is why the default matters.
# Debian trixie packages ngtcp2 1.11 with its GnuTLS binding and MSYS2 has both;
# Homebrew's libngtcp2 links openssl@3 and packages no GnuTLS-bound build, so
# macOS builds with QUIC off and reaches peers over uTP. See the platform table
# in openspec/changes/amule-quic-transport/design.md.
option (ENABLE_QUIC "enable the QUIC NAT-T transport (requires ngtcp2 + its GnuTLS binding)" OFF)

if (NOT NEED_LIB_MULEAPPCORE)
	# uTP lives entirely in the core (the client UDP socket and the client
	# connection path); there is nothing for it to do in a build without one.
	# The same is true of QUIC, which rides the same socket.
	set (ENABLE_UTP FALSE)
	set (ENABLE_QUIC FALSE)
endif()

# Master switch for the in-app "check for a new aMule version" feature: the
# startup notification, the "Check for new version at startup" preference, and
# the About dialog's "Check for updates" button. When OFF the whole feature
# (including the CVersionCheck HTTP code) is compiled out, so nothing contacts
# GitHub and no download links are shown. Packagers shipping aMule via an OS
# package manager want OFF, so the distro's package manager owns updates.
# Standalone / portable / AppImage builds and Windows/macOS want ON.
option (ENABLE_VERSION_CHECK "compile in the in-app new-version check (startup notification + About 'Check for updates'); OFF for OS-package builds" ON)
